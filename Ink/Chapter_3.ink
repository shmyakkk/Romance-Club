=== chapter_3 ===
<pause>
 ->part_3_1

=== part_3_1 ===
#started_bg guestroom
<pause>
ГГ (sad): Нет!
..: Ты резко села на постели, зашарила руками по одеялу.
..: Поняв, что рядом никого нет и тебе не угрожает опасность, ты приоткрыла глаза.
ГГ (surprise): Уже утро? Сколько вообще времени?
..: Ты осмотрелась в поиске часов, но ничего не нашла.
ГГ (dream): Ладно, другой вопрос. Где я?
..: В твоей голове медленно восстанавливалась картина вчерашних событий.
..: И чем больше ты вспоминала, тем сильнее начинало биться сердце. 
ГГ (sad): Проклятье… 
..: Ты схватилась за телефон, чтобы проверить уведомления.
ГГ (sad): Десять звонков от Карла… Наверное, он очень испугался, что я не вернулась.
ГГ (surprise): И еще десятки сообщений. Боюсь их открывать…
..: Не решившись прочитать смски от Карла за ночь, ты убрала телефон подальше.
..: Глубоко задышала, чтобы расставить все на свои места.
ГГ (dream): Мы поссорились, я ушла… приехала к Мартину…
ГГ (dream): Он предложил остаться, поэтому я переночевала здесь. 
ГГ (sad): Но вернуться домой я уже не могу. 
..: Ты встала и принялась одеваться, не переставая думать о проблемах.
..: Что делать?

    + [взбодрить себя]
        ~ alter(professionalism, 1)
        ..: Ты не позволила себе раскисать. 
        ГГ (base): Нет, я все сделала правильно. 
        ГГ (smile): И вообще я молодец! 
        ..: Тебе не хотелось раскисать и оплакивать прошлую жизнь.
        ..: Будущее казалось куда интереснее и ярче. 
        ГГ (evil): У меня все получится. Без вариантов. 
        -> part_3_2

    + [погрустить]
        ..: Ты приуныла, растерявшись от всего, что обрушилось на тебя.
        ГГ (sad): Какая-то череда неприятностей…
        ГГ (sad): Но больше всего я боюсь Карла… или того, что он может сделать.
        ..: Эти мысли пугали, но и деться от них было некуда. 
        ..: Оглядываясь назад, ты поняла, что один за одним пропускала красные флаги. 
        ГГ (sad): Не знаю, выдержу ли я все это…
        -> part_3_2

    + [отвлечься от этих мыслей]
        ~ alter(scandal, 1)
        ..: Ты пожала плечами, отбрасывая переживания.
        ГГ (base): В конце концов, это не могло длиться вечно.
        ГГ (dream): Пусть я и мечтала встретить с Карлом старость, сейчас это окажется одной сплошной ошибкой.
        ..: Быстро взглянув на телефон, ты отвернулась от него.
        ГГ (smile): И вообще Мартин отнесся ко мне очень по-доброму.
        ГГ (dream): Заслужила ли я к себе такое отношение… 
        -> part_3_2

=== part_3_2 ===
..: Твои мысли прервал тихий стук в дверь. 
Мартин (none): (имя гг), ты привела кого-то или сама с собой разговариваешь?
..: По его тону ты не сразу поняла, шутит ли он.
ГГ (evil): «Я что, вслух бубнила?»
ГГ (surprise): Э-э… я тут одна.
Мартин (none): Я зайду?
ГГ (base): Да.
..: Как раз закончив одеваться, ты обернулась и встретилась взглядом с хозяином апартаментов. 
..: Его чуть смущенная улыбка сбила тебя с толку. 
Мартин (smile): Я пошутил. Надеюсь, ты не восприняла всерьез. 
ГГ (smile): Нет. Нет, все в порядке.

{is_kiss_you: 
    ..: Воспоминания о том, что было ночью, заставили тебя покраснеть. 
    Мартин (smile): …
    ГГ (dream): Надеюсь, он забыл, что я к нему приставала.
    ГГ (sad): Как же стыдно.
}

..: Неожиданно он вскинул руку и проверил время на наручных часах. 
Мартин (base): Во сколько начинается твоя вечерняя смена? 
ГГ (dream): В три часа…
..: Хоть Мартин и нахмурился после твоего ответа, ты почувствовала, что он дурачится. 
Мартин (base): Тогда у тебя есть время на завтрак. 
Мартин (base): Не знаю, что конкретно ты предпочитаешь, поэтому заказал несколько доставок.
ГГ (surprise): …
Мартин (base): Идем?
ГГ (smile): Ага. 
#bg home_Martin_morning
<pause>
..: От одного запаха твой желудок предательски заурчал, заставив Мартина усмехнуться.
Мартин (smile): Выбирай, что тебе нравится.
..: Ты взяла…

    + [кусок сырной пиццы]
        ГГ (wide_smile): Я обожаю пиццу! 
        Мартин (base): Надеюсь, я взял твою любимую.
        ..: Ты кивнула, так как рот уже был занят. 
        -> part_3_3

    + [сырники с малиновым вареньем]
        ГГ (surprise): У тебя тут пекарня в доме?
        Мартин (base): Пекарня, парикмахерская, фитнес-зал, ресторан для бизнес-встреч…
        ГГ (dream): Ладно-ладно, я поняла. У тебя здесь есть все.
        Мартин (smile): Почти.
        -> part_3_3

    + [стакан воды]
        ГГ (sad): Я слишком нервничаю, чтобы есть.
        ГГ (base): Но спасибо за заботу.
        ..: Мартин отмахнулся, давая понять, что это не проблема.
        -> part_3_3

=== part_3_3 ===
Мартин (base): Давай обсудим твою новую жизнь.
..: Ты едва не подавилась.
ГГ (dream): А… что именно?
Мартин (base): Например, что ты не сможешь жить в моей квартире всегда.
ГГ (dream): «Боже, скажи мне что-то, чего я не знаю».
ГГ (sad): Я не могу вернуться. По крайней мере пока что.
..: Ты замялась, не зная, как сказать следующее, но все же заставила себя.
ГГ (sad): И у меня нет денег снять себе жилье.
Мартин (base): Я понимаю.
Мартин (base): У нашего агентства есть модельная квартира с парой свободных мест.
..: Твоему удивлению не было предела.
ГГ (surprise): Ты дашь мне там жить бесплатно?
Мартин (surprise): …
ГГ (dream): «Почему он смотрит на меня, как на несмышленого ребенка». 
..: Его взгляд вдруг прояснился и он пробурчал себе под нос.
Мартин (base): С чего я взял, что ты знаешь все эти нюансы.
Мартин (base): Нет, модели не живут бесплатно в модельных апартаментах.
Мартин (base): Они работают и отдают часть гонорара агентству, так и оплачивается проживание.
ГГ (base): Звучит разумно. 
ГГ (dream): «Наверное…»
ГГ (base): Мартин…

    + [ты так в меня веришь]
        ~ alter(professionalism, 1)
        Мартин (base): Я еще никогда не ошибался, когда делал ставки.
        ..: На его лице промелькнула тень гордости. 
        Мартин (flirt): Если я выбрал тебя, то у тебя есть потенциал.
        Мартин (smile): Можешь не сомневаться.
        ..: Тебе показалось, что в его словах было больше самодовольства, чем правды. 
        ГГ (dream): «Доверяй, но проверяй…»
        -> part_3_4

    + [а если я ничего не заработаю?]
        ~ alter(scandal, 1)
        Мартин (base): Такого не будет.
        ГГ (surprise): Вот как?
        Мартин (base): Если даже это случится, то я признаю свою ошибку.
        ..: Его лицо стало непривычно жестче, заставив тебя поежиться.
        Мартин (base): Ты ничего не будешь мне должна. Это будет мне уроком.
        Мартин (smile): Но я верю в тебя.
        ГГ (surprise): …
        ГГ (smile): Спасибо. Последний раз в меня так верили только родители.
        ГГ (sad): «А я их подвела…»
        -> part_3_4

=== part_3_4 ===
Мартин (base): Что ж, думаю нам пора.
..: Он встал из-за стола, ты подорвалась за ним следом.
Мартин (base): Заедем в кофейню, ты уволишься, потом в агентство.
ГГ (surprise): Вот так сразу? 
Мартин (smile): Чем быстрее ты приступишь к работе, тем быстрее мы все с этого заработаем.
Мартин (base): Тебя еще ждет обучение в модельной школе, а на это тоже уйдет время.
ГГ (dream): «Кто мне даст уволиться без отработки?» 
..: Ты не решилась спорить и молча последовала за ним.
#bg taxi_day
<pause>
..: Оказалось, что Мартин не водит машину и предпочитает передвигаться на такси.
..: Всю дорогу ты нервничала, прокручивая в голове возможные диалоги с Джеймсом. 
ГГ (dream): Кстати, что за модельная школа?
Мартин (base): Несколько занятий, которые должны обучить азам модельной работы.
Мартин (base): Как ходить, как позировать, как общаться с клиентами.
ГГ (surprise): И сколько мне предстоит там учиться?
Мартин (base): Недели две.
ГГ (dream): И эти две недели я не смогу работать моделью, так?
Мартин (smile): Может и сможешь, если будешь стараться.
ГГ (dream): Я просто подумала…
..: Тебе было не суждено закончить мысль, так как вы подъехали к кофейне и ты увидела Карла у входа. 
ГГ (sad): О нет… 
..: Ты сползла с места, чтобы он тебя не заметил.
Мартин (base): Идем, еще полно дел сегодня.
ГГ (sad): Я… я не могу.
Мартин (surprise): Почему?
ГГ (sad): Там Карл стоит… парень, с которым я… от которого я…
..: Цепкий взгляд Мартина тут же нашел Карла на пороге. 
Мартин (base): Я понял. Есть другой вход в кофейню?
ГГ (dream): Да, с внутреннего двора. Мусор там выносим.
..: Дав указания таксисту, Мартин повернулся к тебе и вдруг взял тебя за руку.
ГГ (surprise): «Он постоянно берет меня за руку».

    + [Мне это нравится.]
        ~ respect_Martin = respect_Martin + 1
        #info Мартин видит, что ты не против.
        ГГ (smile): «Очень заботливо с его стороны». 
        -> part_3_5

    + [Меня это смущает.]
        ГГ (dream): «Слишком много касаний, а мы едва знакомы».
        -> part_3_5

=== part_3_5 ===
Мартин (smile): Не переживай. 
ГГ (dream): Как ответить?

    + [Я в порядке.]
        ~ alter(professionalism, 1)
        ..: Мартин ободряюще улыбнулся одними уголками губ и чуть сжал твою ладонь.
        -> part_3_6

    + [Пусть он переживает.]
        ~ alter(scandal, 1)
        ..: Мартин удивленно вскинул брови, но не стал отвечать.
        -> part_3_6

=== part_3_6 ===
..: Вы заехали в ближайший поворот с выходом во внутренний двор и наконец покинули такси. 
..: Уговорив Мартина остаться подождать тебя на улице, ты зашла в кофейню через черный вход.
#bg cafe_day
<pause>
..: В кофейне было как обычно людно и шумно в разгар выходного дня. 
..: Джеймс, владелец кофейни, был весь в работе.
ГГ (base): Пс, Джеймс. 
..: Ты выглянула из-за шторки рабочего помещения и попыталась подозвать его, чтобы тебя не было видно с улицы. 
..: Но как назло он тебя не слышал, занятый то гостями, то приготовлением напитков.
ГГ (dream): «Ну и как мне привлечь его внимание?» 
..: Что сделать?

    + [Уронить что-нибудь]
        ~ alter(scandal, 1)
        ..: Ты осмотрелась в поисках того, что может звучно упасть.
        ..: Твой взгляд остановился на питчере для молока.
        ГГ (dream): «Ох, это будет громко».
        ..: Быстрым движением ты скинула его на пол и зажмурилась от устроенного шума.
        ГГ (smile): «Теперь я понимаю, что чувствуют кошки».
        -> part_3_7

    + [Позвать громче]
        ~ alter(professionalism, 1)
        ..: Ты снова высунула нос из-за шторки и как можно громче закашляла в кулак.
        ..: Ноль реакции от Джеймса, он даже не обернулся.
        ГГ (evil): «Да чтоб тебя». 
        ГГ (base): Джеймс!
        ..: На твой возглас обернулся уже не только Джеймс, но и все гости в очереди. 
        ..: К счастью, ты успела юркнуть обратно, прежде чем кто-либо разглядел тебя.
        -> part_3_7

=== part_3_7 ===
Джеймс (base): Я сейчас вернусь.
..: Его место занял второй бариста, позволив Джеймсу отойти к тебе.
Джеймс (surprise): (имя гг)? Ты не рановато?
ГГ (base): Джеймс, у меня серьезный разговор.
Джеймс (smile): Ты меня пугаешь. 
..: Тебе вдруг стало грустно, потому что ты не так уж хотела увольняться.
..: Ты любила эту кофейню, атмосферу и своих коллег.
..: Все происходило слишком быстро.
ГГ (evil): «Ну же! Произнеси это уже!»
ГГ (base): Джеймс, помнишь того парня, который вчера разговаривал со мной за барной стойкой?
Джеймс (base): Да.
ГГ (base): Он предложил мне работу и… я согласилась. 
ГГ (sad): Поэтому я увольняюсь.

{is_tell_true_James:
    ..: Ты боялась смотреть ему в глаза, но со стороны Джеймса раздался лишь печальный вздох.
    Джеймс (sad): Почему-то, я был уверен, что так и будет.
    Джеймс (smile): Ты всегда была слишком хороша для работы здесь.
    ..: Вы оба рассмеялись, сбрасывая напряжение.
    ГГ (flirt): Ну ты и льстец.
    Джеймс (smile): Нет, правда. Кто теперь втюхает гостям сиропы и сливки, хлопая глазками?
    ГГ (surprise): Да никогда я так не делала!
    Джеймс (surprise): Средний чек в твои смены говорит об обратном!
    ..: Ты улыбнулась, но улыбка быстро погасла.

- else:
    Джеймс (base): Ха! Теперь понятно, почему ты не стала говорить правду вчера.
    ГГ (sad): …
    Джеймс (base): Ладно, что с тобой делать. 
    Джеймс (base): Я не могу удерживать тебя насильно.
}

ГГ (base): Прости, что я так резко.
ГГ (sad): Вчера после смены произошло еще кое-что…
..: Прищурившись, Джеймс перевел взгляд с тебя на шторку, закрывающую вас от гостей.
..: А потом приоткрыл ее, кивая головой на вход в кофейню, где стоял твой парень.
..: Бывший парень.
Джеймс (evil): Я так понимаю, произошел Карл?
ГГ (surprise): Как отреагировать?

    + [Спокойно]
        ~ alter(professionalism, 1)
        ..: Тебе понадобилось все самообладание, чтобы не выругаться.
        ГГ (base): Да. Именно он.
        -> part_3_8

    + [Бурно]
        ~ alter(scandal, 1)
        ..: Ты растерялась и сказала первое, что пришло в голову.
        ГГ (evil): Да, черт возьми, произошел Карл! 
        ГГ (sad): А теперь он еще и преследует меня!
        -> part_3_8

=== part_3_8 ===
Джеймс (evil): Не удивлен. Я понял, что этот парень не в себе, еще когда он разбил стакан в тот раз.
Джеймс (base): Помнишь, когда поранилась гостья, сидевшая рядом с вами?
ГГ (sad): Это была случайность…
Джеймс (base): Да…
Джеймс (evil): (имя гг), он сделал тебе больно?
..: Ком застрял в горле, мешая ответить, но Джеймс вдруг отвлекся на что-то в зале.
Джеймс (surprise): А это не твой ли новый работодатель болтает с Карлом? 
..: Ты тут же откинула штору, позабыв об осторожности, чтобы увидеть их своими глазами.
ГГ (surprise): О, нет-нет-нет.
Джеймс (base): Дай угадаю, ты не планировала их знакомить?
ГГ (surprise): Я должна вмешаться.
Джеймс (evil): Уверен, они разберутся и без тебя.
..: Но ты уже спешила на улицу, где на повышенных тонах общались Карл и Мартин.
#bg cafe_street_day
<pause>
Карл (evil): Слышь, щегол, уйди с дороги.
Мартин (evil): Я еще раз повторяю, что вашу ситуацию необходимо разрешить мирно.
..: Увидев тебя, лицо Карла исказилось ненавистью. 
..: Твоя уверенность пошатнулась, когда он рявкнул тебе в лицо.
Карл (evil): Неужели, явилась! Где ты шлялась всю ночь?!
ГГ (surprise): …

    + [ответить самой]
        ~ respect_Martin = respect_Martin + 1
        #info Мартин впечатлен твоей смелостью.
        ..: Ты заставила себя не отводить взгляд, прямо смотрела в глаза, полные ярости.
        ГГ (base): Это не имеет значения.
        ГГ (evil): Мы расстались с тобой, Карл.
        ..: Злое выражение на его лице сменилось презрением.
        Карл (base): Сбежала к своему любовнику, какому-то холеному петуху?
        Мартин (evil): Я бы попросил…
        Карл (evil): Заткни варежку. 
        ГГ (evil): Карл, прекрати эту сцену. Зачем ты вообще сюда пришел?
        Карл (base): Ты тупая? Тебя ждал, чтобы привести в чувство. 
        ГГ (evil): Я уже все сказала. Иди домой!
        Карл (evil): Только с тобой, милая!
        -> part_3_9

    + [позволить Мартину вступиться]
        ..: Ты отшатнулась к Мартину и с мольбой обернулась на него. 
        Карл (evil): Что ты вылупилась на этого придурка? 
        Карл (surprise): Неужели твой любовничек, к которому ты решила уйти?
        Мартин (base): Вообще-то, я модельный скаут и (имя гг) приняла мое предложение о работе в нашем агентстве. 
        ..: От упоминания агентства голова Карла как-то странно дернулась, а взгляд уперся в Мартина. 
        Карл (evil): Так это ты лезешь не в свое дело? 
        Мартин (base): Эту прекрасную девушку ждет блистательное будущее.
        Мартин (evil): Без вас. 
        Карл (surprise): Да что ты о себе возомнил, упырь?
        ГГ (sad): Карл, успокойся. 
        Карл (base): А ты вообще помолчала бы, дура. 
        Карл (evil): Какой-то пижон просто решил затащить тебя в койку, как молоденькую лохушку.
        -> part_3_9

=== part_3_9 ===
..: И вдруг он сделал резкий выпад вперед, замахнулся в твою сторону.
..: Ты сжалась, вскинув руки перед собой, чтобы защититься.
..: Но удара не последовало.
..: Ты неуверенно приоткрыла один глаз.
Мартин (evil): …
ГГ (surprise): Мартин…
..: Мартин перехватил занесенную для удара руку Карла, крепко сжал его запястье.
..: Они сверлили друг друга напряженными взглядами. 
Карл (base): Еще раз повторяю — не лезь не в свое дело.
Мартин (base): Воспитание не позволяет мне допустить рукоприкладства в отношении девушки.
Карл (evil): Тогда получай сам!
..: Увы, Карлу не удалось продемонстрировать свои навыки рукопашного боя.
Джеймс (evil): …
..: Из кофейни выскочил Джеймс и в мгновение ока перехватил вторую руку Карла.
..: А потом и вовсе ловко выкрутил обе его руки ему за спину.
ГГ (surprise): …
..: Несмотря на двух удерживающих его мужчин, Карл смотрел прямо на тебя.
Карл (evil): Со всеми тут трахалась у меня за спиной? Потаскуха.
Джеймс (evil): Следи за языком. И не распугивай мне гостей.
..: Джеймс встряхнул его, но Карл не отрывал от тебя прожигающего насквозь взгляда.
ГГ (sad): «Кажется, вещи из дома я не заберу». 
Карл (base): Тебя никто не будет любить так, как я.
Карл (evil): Никому ты не нужна кроме меня!
Мартин (base): Концерт окончен, я вызываю полицию. 
Джеймс (base): Отличное решение.
ГГ (surprise): Нет, стойте.
..: Три пары глаз уставились на тебя, но ты не дрогнула. 
ГГ (base): Карл…

    + [уйдет сам]
        ~ alter(professionalism, 1)
        ГГ (base): И не будет доставлять никому неудобств.
        Карл (smile): …
        ГГ (evil): Так ведь, Карл?
        Карл (base): Ага. 
        ..: Кажется, твою вежливость он принял за жалость из-за чувств, но тебе было все равно.
        ГГ (base): Вот и славно. Отпустите его.
        ГГ (dream): Пожалуйста.
        Джеймс (surprise): Ты уверена?
        ..: Ты еще раз посмотрела на Карла и кивнула.
        ..: Когда его руки оказались свободны, он раздраженно потер затекшие запястья и вдруг неприятно ухмыльнулся тебе.
        Карл (flirt): Еще увидимся, милая. 
        ГГ (evil): Обязательно.
        -> part_3_10

    + [должен сначала извиниться]
        ~ alter(scandal, 1)
        ГГ (base): Я жду, Карл.
        Карл (surprise): Ты что, совсем рехнулась? 
        ..: Джеймс сильнее сдавил ему руки, вынуждая наклониться.
        Карл (evil): Да отвали ты! 
        ГГ (evil): Иначе ты уедешь отсюда с полицией, Карл! 
        Карл (evil): …
        ГГ (flirt): Извиняться это не больно.
        ..: Его рот кривился, выбирая между проклятьем в твой адрес и извинением. 
        ..: Оценив ситуацию, Карл все-таки выдавил из себя одно едва различимое слово.
        Карл (base): Извини.
        Джеймс (smile): Как-то не очень искренне, (имя гг), тебе не кажется?
        ГГ (base): Это лучшее на что он способен. Отпустите его. 
        ..: Когда его руки оказались свободны, он раздраженно потер затекшие запястья и бросил на тебя взгляд исподлобья.
        Карл (evil): Тебе это с рук не сойдет, милая.
        ГГ (evil): Прекрати угрожать мне.
        -> part_3_10

=== part_3_10 ===
..: Оглядев тебя и твоих защитников, Карл был вынужден признать поражение и все-таки ушел.
ГГ (dream): «Сколько еще проблем от него будет?»
Джеймс (smile): Я Джеймс.
Мартин (smile): Мартин. Приятно познакомиться.
Джеймс (base): Я бы сказал взаимно, но вы решили украсть мою сотрудницу.
ГГ (surprise): Джеймс!
Джеймс (smile): Да, именно так меня зовут.
..: Мартин шутливо слегка наклонил голову, сдаваясь перед твоим управляющим, и снова выпрямился.
Мартин (smile): Виновен. Надеюсь, вы уже договорились.
Джеймс (base): Ну, сегодня (имя гг) должна отработать хотя бы последнюю смену.
Джеймс (base): Но так и быть, я готов отпустить ее без двухнедельной отработки.
ГГ (smile): Спасибо, Джеймс. 
Мартин (base): Можно ли как-то решить этот вопрос, чтобы сегодня она тоже не работала? 
Джеймс (base): Если только согласится выйти кто-то из других ребят. 
ГГ (dream): Давайте уже зайдем в кофейню.
#bg cafe_day
<pause>
..: Ты быстро проверила время, считая, сколько часов до смены оставалось.
ГГ (dream): «Как мне решить проблему?»

    + [d10 попросить кого-то из коллег выручить]
        ~ respect_James = respect_James + 1
        #info Ваши отношения с Джеймсом улучшились.
        ГГ (dream): «Сегодня выходной. Выбор отстой, но попробовать стоит».
        ГГ (base): Позвоню…
        
        -- (who)
            ** [Мэри]
                ГГ (dream): Она меня точно пошлет, я ей не нравлюсь.
                ..: Ты ждала и ждала, пока в трубке раздавались гудки.
                ГГ (base): Ладно, попробую еще раз.
                ..: Но второй твой звонок Мэри просто сбросила.
                ГГ (dream): Что ж, это было ожидаемо.
                ГГ (base): Позвоню…
                -> who
                
            ** [Дэри]
                ГГ (dream): В отличие от своей сестры, она всегда была ко мне добра. 
                Дэри (none): Алло?
                ГГ (smile): Привет, это (имя гг). У меня чрезвычайная ситуация, можешь выручить?
                Дэри (none): Что случилось?
                ГГ (base): Не могу сегодня выйти на смену вечернюю. Вот ищу, кто подменит.
                Дэри (none): Прости, (имя гг), но я никак, я на все выходные за городом.
                ГГ (sad): Эх, ничего. Вдруг ты смогла бы.
                Дэри (none): Может, в другой раз. 
                ГГ (dream): «Его уже не будет».
                Дэри (none): Кстати, Мэри передает тебе привет.
                ГГ (surprise): … 
                ..: Вы попрощались, закончив звонок.
                ГГ (base): Позвоню…
                -> who
                
            ** [Карлу]
                ГГ (dream): «Ну вот, я уже схожу с ума».
                ..: Ты убрала палец с имени бывшего парня на экране. 
                ГГ (base): Позвоню…
                -> who
            
            ** [новенькому]
                ГГ (sad): Так неловко, я забыла, как его зовут. 
                Новенький (none): Алоха, это кто? 
                ГГ (base): Привет, это (имя гг). Мы с тобой в одной кофейне работаем.
                Новенький (none): Ваще не помню. Но верю!
                ГГ (base): Я не смогу сегодня выйти на вечернюю смену, очень нужно, чтобы ты подменил. Сможешь?
                ..: Пока он думал, ты успела сгрызть себе ноготь на указательном пальце.
                Новенький (none): Ладно, забились.
                ГГ (surprise): Правда?	
                Новенький (none): Деньги лишними не бывают, да и все равно делать нечего.
                ГГ (wide_smile): Спасибо огромное! Я скажу Джеймсу.
                Новенький (none): Ага. Бывай.
                ..: И бросил трубку, но ты уже летела на крыльях свободы к управляющему.
                Джеймс (base): Ну услужила, вытащила Тома, который еще ничего толком не умеет.
                ГГ (sad): Джеймс, я…
                Джеймс (base): Да ничего, разберемся.
                -> part_3_11

    + [умолять Джеймса]
        ~ respect_James = respect_James - 1
        #info Ваши отношения с Джеймсом ухудшились.
        ГГ (sad): Джеймс, ну кто согласится выйти в выходной.
        ГГ (sad): Все наверняка заняты.
        Джеймс (evil): …
        Джеймс (base): Ты ведь оставишь нас без одного человека в вечернюю смену.
        Джеймс (base): Сама знаешь, как вечером много гостей.
        ГГ (sad): Знаю-знаю, но я правда не могу.
        ГГ (sad): Совсем. Никак.
        ..: Тебе было стыдно, но ты уже не могла представить себе, как наденешь фартук. 
        Джеймс (base): Ладно. Но только потому, что ты всегда была ответственным сотрудником.
        ГГ (wide_smile): Спасибо!
        -> part_3_11

=== part_3_11 ===
Джеймс (smile): (имя гг), я буду скучать. Заходи к нам хоть иногда.
ГГ (smile): Конечно. Спасибо тебе за все.
ГГ (wide_smile): Это был лучший год.
..: Между вами словно из ниоткуда вырос Мартин.
Мартин (flirt): Я уверен, лучшие годы еще впереди.
ГГ (smile): Первая работа навсегда останется в сердце.
Мартин (smile): Еще раз рад знакомству, Джеймс.
Джеймс (smile): Взаимно.
..: Мужчины пожали друг другу руки, и вы с Мартином вышли из кофейни.
..: Ты с тоской посмотрела на свое уже бывшее место работы, пока садилась в такси.
#bg taxi_day
<pause>
Мартин (base): Едем в агентство, оформим твое обучение. 
Мартин (base): Вечером поедешь в модельные апартаменты. 
Мартин (smile): Заодно познакомишься с другими моделями.
..: Ты молча кивнула и отвернулась к окну, бросая последний взгляд на кофейню.
ГГ (sad): «Слишком много бывших за один день». 

#end
<pause>
-> END




